{
  // https://github.com/mna/pigeon
  // go install github.com/mna/pigeon@latest
  // pigeon -o parser.go grammar.peg

  package main

  import (
    "fmt"
    "strconv"
  )

  func Join(bytes interface{}) string {
    result := ""
    for _, b := range bytes.([]interface{}) {
      result += string(b.([]uint8))
    }
    return result
  }

  func GetList[T any](list interface{}) []T {
    result := []T{}

    for _, raw := range list.([]interface{}) {
      result = append(result, raw.(T))
    }

    return result
  }
}

chain = _ ch: (comment/call_args/call/pipe)* _ {
  return GetList[Call](ch), nil
}

call_args = _ i: ident ":"? v: value _ {
	file, _ := PATH_STACK.Peek(0)
  return Call{i.(string), v.(Literal), file, c.pos.line}, nil
}

call = _ i: ident _ {
	file, _ := PATH_STACK.Peek(0)
  return Call{i.(string), Nada, file, c.pos.line}, nil
}

value = _ v: (pair/expression/nada/number/hashtag/text/logical/list/function/kind) _ {return v, nil}
value_no_pair = _ v: (expression/nada/number/hashtag/text/logical/list/function/kind) _ {return v, nil}

ident = _ i: [a-zA-Z0-9_/]+ _ {
  return Join(i), nil
}

expression = _ "(" _ ch: chain _ ")" _ {
  return Literal{ch, Kind{name: EXPRESSION}}, nil
}

function = _ r: "%"? _ "{" _ f: chain _ "}" _ {
	fn := CreateFunction(f.([]Call), (r != nil))
	fn.file, _ = PATH_STACK.Peek(0)
	fn.line = c.pos.line
	return Literal{fn, Kind{name: FUNCTION}}, nil
}

list = _ "[" _ l: value* _ "]" _ {
  list := GetList[Literal](l)
  spec := DetermineListSpec(list)
  return Literal{list, Kind{name: LIST, spec1: &spec}}, nil
}

pair = _ l: value_no_pair "&" r: value_no_pair _ {
  left := l.(Literal)
  right := r.(Literal)
  return Literal{Pair{left, right}, Kind{PAIR, &left.kind, &right.kind}}, nil
}

kind = _ "@" k: ("any"/"nada"/"text"/"number"/"logical"/pair_kind/list_kind/"function"/"type") _ {
  switch val := k.(type) {
  case []uint8:
	  return Literal{Kind{name: string(val)}, Kind{name: TYPE}}, nil
  case Kind:
	  return Literal{val, Kind{name: TYPE}}, nil
  default:
    return Nada, nil
  }
}

list_kind = _ "list(" _ spec: kind _ ")" _ {
  lit := spec.(Literal)
  finalSpec := lit.value.(Kind)
  return Kind{name: LIST, spec1: &finalSpec}, nil
}

pair_kind = _ "pair(" _ left: kind _ "&" _ right: kind _  ")" _ {
  leftLit := left.(Literal)
  finalLeft := leftLit.value.(Kind)
  rightLit := right.(Literal)
  finalRight := rightLit.value.(Kind)
  return Kind{name: PAIR, spec1: &finalLeft, spec2: &finalRight}, nil
}

number = _ n: [0-9]+ _ {
  num, _ := strconv.ParseInt(Join(n),10,0)
  return Literal{num, Kind{name: NUMBER}}, nil
}

hashtag = _ "#" t: ident _ {
  return Literal{t, Kind{name: TEXT}}, nil
}

text = _ "\"" t: [^"]* "\"" _ {
  return Literal{Join(t), Kind{name: TEXT}}, nil
}

logical = _ l: (truth/lie) _ {return Literal{l, Kind{name: LOGICAL}}, nil}
truth = ("true"/"OK") {return true, nil}
lie = ("false"/"FAIL") {return false, nil}

nada = _ "nada" _ {
  return Nada, nil
}

comment = _ "--" [^\n\r]* _ {
	file, _ := PATH_STACK.Peek(0)
	return Call{"comment", Nada, file, c.pos.line}, nil
}

pipe = ">" {
	file, _ := PATH_STACK.Peek(0)
	return Call{"pipe", Nada, file, c.pos.line}, nil
}

_ = [ \n\r\t]*

